plugins {
    java
    id("org.springframework.boot") version "3.5.16"
    id("io.spring.dependency-management") version "1.1.7"
    id("org.danilopianini.gradle-java-qa") version "1.165.0"
    // Opt-in Native Image support. The JVM bootJar and Dockerfile remain the default path.
    id("org.graalvm.buildtools.native") version "0.10.6"
}

group = "com.hotelpms"
version = "0.0.1-SNAPSHOT"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

springBoot {
    mainClass.set("com.hotelpms.frontdesk.FrontdeskApplication")
}

graalvmNative {
    binaries {
        named("main") {
            if (providers.gradleProperty("nativeQuickBuild").orNull == "true") {
                // Fast reachability gate; the dispatch workflow runs the final optimized gate separately.
                buildArgs.add("-Ob")
            } else {
                // Explicit final optimization so the optimized gate is visible in CI evidence.
                buildArgs.add("-O2")
            }
            // Quotation rendering reaches PDFBox's headless java.desktop path.
            buildArgs.add("-Djava.awt.headless=true")
            // GraalVM emits AWT JNI libraries beside the executable in this
            // supported dynamic-library layout.
            buildArgs.add("-H:+StaticExecutableWithDynamicLibC")
            providers.gradleProperty("nativeAwtConfigDir").orNull?.takeIf { it.isNotBlank() }?.let {
                buildArgs.add("-H:ConfigurationFileDirectories=$it")
            }
            buildArgs.add("-J-Xmx12g")
            buildArgs.add("--parallelism=2")
            buildArgs.add("-H:DeadlockWatchdogInterval=60")
        }
    }
}

configurations {
    compileOnly {
        extendsFrom(configurations.annotationProcessor.get())
    }
}

repositories {
    mavenCentral()
}

ext {
    set("springCloudVersion", "2025.0.0")
    // CVE-2026-42583/42584/42579/42587: fixed in 4.1.133.Final; CVE-2026-47691/45674/45416/44249: fixed in 4.1.135.Final; CVE-2026-56745/55833/55831/59901: fixed in 4.1.136.Final — override Spring Boot BOM pin.
    set("netty.version", "4.1.136.Final")
    set("mapStructVersion", "1.6.3")
    // CVE-2026-43512/43513/43515/41284/41293/42498: fixed in Tomcat 10.1.55 (2026-05-05).
    set("tomcat.version", "10.1.55")
}

dependencies {
    implementation(project(":internal-auth-lib"))
    implementation(project(":common-web-lib"))
    // Quotation PDF export — same renderer billing-service uses for invoices.
    implementation(project(":pdf-template-engine"))
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")
    implementation("org.springframework.boot:spring-boot-starter-validation")
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-security")
    // Redis-backed nonce store for internal HMAC anti-replay (T-GW-08)
    implementation("org.springframework.boot:spring-boot-starter-data-redis")
    implementation("org.springframework.cloud:spring-cloud-starter-config")

    // --- Feign: only guest-service and billing-service remain external after the
    // inventory/reservation/stay merge (room/reservation calls are now in-process). ---
    implementation("org.springframework.cloud:spring-cloud-starter-openfeign") {
        exclude(group = "org.springframework.cloud", module = "spring-cloud-starter-netflix-eureka-client")
    }
    implementation("org.springframework.cloud:spring-cloud-starter-loadbalancer")
    implementation("org.springframework.cloud:spring-cloud-starter-circuitbreaker-resilience4j")

    // --- Database migration (Flyway) ---
    implementation("org.flywaydb:flyway-core")
    runtimeOnly("org.flywaydb:flyway-database-postgresql")

    // --- Caching (RoomType reference data + Alloggiati lookup tables) ---
    implementation("org.springframework.boot:spring-boot-starter-cache")
    implementation("com.github.ben-manes.caffeine:caffeine")

    // --- Observability ---
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("io.micrometer:micrometer-tracing-bridge-brave")
    implementation("io.zipkin.reporter2:zipkin-reporter-brave")
    runtimeOnly("io.micrometer:micrometer-registry-prometheus")

    // --- GAP-4: Log aggregation SIEM (Loki via logback appender) ---
    implementation("com.github.loki4j:loki-logback-appender:1.5.2")

    compileOnly("org.projectlombok:lombok:1.18.38")
    annotationProcessor("org.projectlombok:lombok:1.18.38")

    implementation("org.mapstruct:mapstruct:${property("mapStructVersion")}")
    annotationProcessor("org.mapstruct:mapstruct-processor:${property("mapStructVersion")}")

    // --- OpenAPI / Swagger UI ---
    implementation("org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.4")

    runtimeOnly("org.postgresql:postgresql")

    // --- CSV parsing for Alloggiati Web lookup table downloads ---
    // PINNED at 1.9.0 — DO NOT upgrade to 1.10.0+ without verifying commons-io compatibility.
    // See dependencyManagement below: commons-io is forced at 2.14.0 (CVE-2024-47554), and
    // commons-csv 1.10.0+ requires commons-io >= 2.15.0 (UnsynchronizedBufferedReader),
    // causing a NoClassDefFoundError in AlloggiatiCsvParser if upgraded alone.
    // Dependabot is configured to ignore commons-csv >= 1.10 (.github/dependabot.yml).
    implementation("org.apache.commons:commons-csv:1.9.0")

    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("net.bytebuddy:byte-buddy:1.15.11")
    testImplementation("net.bytebuddy:byte-buddy-agent:1.15.11")
    testImplementation("org.mockito:mockito-core:5.15.2")
    testImplementation("org.mockito:mockito-junit-jupiter:5.15.2")
    testImplementation("org.springframework.boot:spring-boot-testcontainers")
    testImplementation("org.testcontainers:junit-jupiter")
    testImplementation("org.testcontainers:postgresql")
    // ADR-004: enforces hotel_id scoping on multi-tenant repositories (T-BILL-04 class of bug) —
    // TenantIsolationRules + archunit-junit5 come transitively via the testFixtures below.
    testImplementation(testFixtures(project(":internal-auth-lib")))
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

dependencyManagement {
    imports {
        mavenBom("org.springframework.cloud:spring-cloud-dependencies:${property("springCloudVersion")}")
    }
    dependencies {
        // CVE-2025-48976 (commons-fileupload 1.5→1.6.0) + CVE-2024-47554 (commons-io 2.11.0→2.14.0)
        // commons-fileupload is not managed by Spring Boot 3.5.x BOM (removed with CommonsMultipartResolver
        // in Spring 6.1); dependencyManagement.dependencies forces the version regardless of BOM properties.
        dependency("commons-fileupload:commons-fileupload:1.6.0")
        dependency("commons-io:commons-io:2.14.0")
        // CVE-2026-42198: fixed in PostgreSQL JDBC 42.7.11; CVE-2026-54291 (SCRAM-SHA-256-PLUS channel-binding downgrade): fixed in 42.7.12.
        dependency("org.postgresql:postgresql:42.7.12")
        // CVE-2026-5598: fixed in BouncyCastle 1.84.
        dependency("org.bouncycastle:bcprov-jdk18on:1.84")
    }
}

tasks.withType<Test> {
    useJUnitPlatform()
    systemProperty("net.bytebuddy.experimental", "true")
}

// SpotBugs: project-specific exclusions (Spring DI beans — EI_EXPOSE_REP2 not applicable)
tasks.withType<com.github.spotbugs.snom.SpotBugsTask>().configureEach {
    extraArgs.addAll(
        listOf("-exclude", "${project.projectDir}/config/spotbugs/exclude.xml")
    )
}

// AOT test sources are framework-generated and are not part of the JVM regression
// suite. Disabling their QA avoids resolving the remote Config Server twice.
tasks.matching {
    it.name in setOf(
        "processTestAot",
        "compileAotTestJava",
        "processAotTestResources",
        "aotTestClasses",
        "checkstyleAot",
        "checkstyleAotTest",
        "pmdAot",
        "pmdAotTest",
        "spotbugsAot",
        "spotbugsAotTest"
    )
}.configureEach {
    enabled = false
}
